prototype(CodeQ.Link:Tag) < prototype(Neos.Fusion:Component) {
    # link configuration via transfer object
    linkSource = CodeQ.Link:Link
    class = false

    # tag rendering configuration
    renderDefaultTagIfNoLink = false
    defaultTagName = 'span'
    content = false
    attributes = Neos.Fusion:DataStructure

    shouldRenderLinkTag = ${this.linkSource.enforceNoLinkTag ? false : this.linkSource.link}
    renderer = Neos.Fusion:Tag {
        @if.hasLink = ${props.linkSource.link || props.renderDefaultTagIfNoLink}
        tagName = ${props.shouldRenderLinkTag ? 'a' : props.defaultTagName}
        attributes = Neos.Fusion:DataStructure {
            href = ${props.shouldRenderLinkTag ? props.linkSource.link : false}
            rel = ${props.shouldRenderLinkTag ? props.linkSource.rel : false}
            target = ${props.shouldRenderLinkTag ? props.linkSource.target : false}
            class = ${props.class == false ? false : String.trim(props.class)}
        }
        content = ${props.content || props.linkSource.label}
    }
}
